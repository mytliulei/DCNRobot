*** Settings ***
Suite Setup       init suite test
Suite Teardown    uninit suite test
Resource          resource_reboot.txt

*** Test Cases ***
01 冷启动测试
    [Setup]    01_init
    Log Step Start    step1    重启前发送ixia流量
    Set ixia stream ip    @{testerp1}    stream_rate=1000    stream_rate_mode=${IXIA_StreamRateMode_pps}    dst_mac=00:00:00:22:22:22    src_mac=00:00:00:11:11:11    reset=True
    Set ixia stream ip    @{testerp2}    stream_rate=2000    stream_rate_mode=${IXIA_StreamRateMode_pps}    dst_mac=00:00:00:11:11:11    src_mac=00:00:00:22:22:22    reset=True
    Ixiasend.Start Transmit    @{testerp1}
    Ixiasend.Start Transmit    @{testerp2}
    sleep    5s    #wait for counter
    ${res1}=    CheckIxiaSendReceive    @{testerp1}    @{testerp2}    1    mode=pps
    ${res2}=    CheckIxiaSendReceive    @{testerp2}    @{testerp1}    1    mode=pps
    Ixiasend.Stop Transmit    @{testerp1}
    Ixiasend.Stop Transmit    @{testerp2}
    Should Be True    ${res1}==${res2}==True    tp1与tp2互相收到流量
    Log Step End    step1
    #
    #
    Log Step Start    step2    通过apc将s1p1断电后,重新加电,加电过程中交换机不能转发流量,检查启动时间应该在2分钟内
    ${setOff}=    Set Apc Port    ${apc_alias}    ${s1_apc_port}    OFF
    Run Keyword If    ${setOff}!=${True}    Fail    设置APC OFF异常
    Ixiasend.Clear Statics    @{testerp1}
    Ixiasend.Clear Statics    @{testerp2}
    Ixiasend.Start Transmit    @{testerp1}
    Ixiasend.Start Transmit    @{testerp2}
    #上电启动
    ${setOn}=    Set Apc Port    ${apc_alias}    ${s1_apc_port}    ON
    Run Keyword If    ${setOn}!=${True}    Fail    设置APC ON异常
    #上电时间
    ${secs1}=    Get Time    epoch
    #STP配置恢复完成时间
    Get Debug Output Until Regexp    ${s1_alias}    MSTP is enabled    timeout=20 minute
    ${secs2}=    Get Time    epoch
    @{ret}=    Get Debug Output Until Regexp    ${s1_alias}    ${sw_prompt}    timeout=2minute
    #配置恢复完成时间
    ${secs3}=    Get Time    epoch
    Ixiasend.Stop Transmit    @{testerp1}
    Ixiasend.Stop Transmit    @{testerp2}
    sleep    1min    等待剩余打印信息
    Read    # 读取剩余内容
    #处理相关结果
    ${upTime}=    Evaluate    ${secs3}-${secs1}    #启动时间长度
    ${transTime}=    Evaluate    ${secs3}-${secs2}-30    #stp配置恢复后，端口使能stp功能30s后(stp缺省30s收敛)，开始转发流量
    ${tp1_tx}    ${tp1_rx}=    Get Statics    @{testerp1}    txpackets    rxpackets
    ${tp2_tx}    ${tp2_rx}=    Get Statics    @{testerp2}    txpackets    rxpackets
    ${res1}=    Evaluate    ${tp2_rx}<=1000*${transTime}*1.5 or 0>=${transTime}
    ${res2}=    Evaluate    ${tp1_rx}<=2000*${transTime}*1.5 or 0>=${transTime}
    Run Keyword And Continue On Failure    Should Be True    ${ret[0]}==True    设备在22min内启动失败!!
    Run Keyword And Continue On Failure    Should Be True    ${res1}==${res2}==True    重启过程中,不应该转发流量
    Run Keyword And Continue On Failure    Should Be True    ${upTime}<=${120}    盒式设备启动时间应在120s内
    Log Step End    step2
    #
    #
    Log Step Start    step3    冷启动后,show version显示本次启动为cold start
    Exec Cmd List    ${s1_alias}
    ${ret}=    Exec Cmd    ${s1_alias}    show version
    ${res}=    Tools.Is Match From String    ${ret}    (?i)Last reboot is cold reset
    Should Be True    ${res}    上电启动后,应显示为cold reset!
    Log Step Start    step3
    [Teardown]    01_uninit

*** Key Words ***
01_init
    tools.Comment    ${SUITE_NAME}    ${TEST_NAME}    初始化配置>>>>>
    #打开APC Console
    Telnet.Open Connection    ${apc_ccm_ip}    ${apc_alias}    ${apc_ccm_port}    timeout=10    prompt_is_regexp=${True}    prompt=(?m)^>.*
    ...    default_log_level=${sw_default_log_level}    terminal_emulation=${False}    terminal_type=${sw_terminal_type}    monitor=${sw_monitor}    newline=${sw_newline}
    ${isLogin}=    log In APC    ${apc_alias}
    Run Keyword If    ${isLogin}!=${True}    Fail
    Exec Cmd List In ConfigMode    ${s1_alias}    spanning-tree
    Write Config    ${s1_alias}
    sleep    30s    wait for stp
    tools.Comment    ${SUITE_NAME}    ${TEST_NAME}    初始化配置完成<<<<<

01_uninit
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    恢复初始配置>>>>>
    Exec Cmd List In ConfigMode    ${s1_alias}    no spanning-tree
    Write Config    ${s1_alias}
    Ixiasend.Set Port Mode Default    @{testerp1}
    Ixiasend.Set Port Mode Default    @{testerp2}
    Ixiasend.Stop Transmit    @{testerp1}
    Ixiasend.Stop Transmit    @{testerp2}
    tools.comment    ${SUITE_NAME}    ${TEST_NAME}    恢复初始结束<<<<<
    Close Console    ${apc_alias}
